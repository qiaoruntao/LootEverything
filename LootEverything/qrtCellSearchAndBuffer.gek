scn qrtCellSearchAndBuffer

;static variables
ref eachRef
ref currentCell
;temp variables
int searchCount
ref tempCell
int totalSearchNum
;temp loot use 
int isLegal
ref base
int tempType

Begin Function {totalSearchNum}
	;get from variables
	set eachRef to qrtGlobalMCMVariable.eachRef
	;main function
	set tempCell to player.gpc
	;printtoconsole "tempcell= %n, currentCell=%n" tempCell, qrtGlobalMCMVariable.currentCell
	;printtoconsole "ref=%n" eachRef
	if qrtGlobalMCMVariable.currentCell != tempCell
		;printtoconsole "init now"
		set qrtGlobalMCMVariable.currentCell to tempCell
		set eachRef to GetFirstRef 201 1 0
	elseif IsReference eachRef ==0
		;printtoconsole "not a ref now"
		set eachRef to GetFirstRef 201 1 0
	endif
	set searchCount to 0
	while eachRef != 0 && (totalSearchNum==0 ||searchCount<totalSearchNum)
		;printtoconsole "while check %n now" eachRef
		;add count
		let searchCount +=1
		;check the distance
		if qrtGlobalMCMVariable.distanceCheck!=0
			if player.GetDistance eachRef >qrtGlobalMCMVariable.distanceCheck
				;printtoconsole "distanceCheck continue"
				set eachRef to GetNextRef
				continue
			endif
		endif
		;check if dropped by player
		if qrtGlobalMCMVariable.isPickPlayerDroppedItems==0
			if GetSourceModIndex eachRef ==255
				set eachRef to GetNextRef
				continue
			endif
		endif
		;base is used multiple times below
		set base to eachRef.GetBaseObject
		;check if playable
		if qrtGlobalMCMVariable.isPickUnPlayableItem==0
			if IsPlayable base==0
				set eachRef to GetNextRef
				continue
			endif
		endif
		;check if steal
		let isLegal := call qrtIsLegalToPickUpFunction eachRef
		if isLegal==0
			set eachRef to GetNextRef
			continue
		endif
		;check if quest item
		if qrtGlobalMCMVariable.isPickQuestItem==0
			if IsQuestItem base
				set eachRef to GetNextRef
				continue
			endif
		endif
		;proceed according item type
		set tempType to eachRef.GetType
		if tempType == 24
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer24 qrtGlobalMCMVariable.isDisableMsg24
		elseif tempType == 25
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer25 qrtGlobalMCMVariable.isDisableMsg25
		elseif tempType == 26
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer26 qrtGlobalMCMVariable.isDisableMsg26
		elseif tempType == 29
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer29 qrtGlobalMCMVariable.isDisableMsg29
		elseif tempType == 31
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer31 qrtGlobalMCMVariable.isDisableMsg31
		elseif tempType == 40
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer40 qrtGlobalMCMVariable.isDisableMsg40
		elseif tempType == 41
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer41 qrtGlobalMCMVariable.isDisableMsg41
		elseif tempType == 46
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer46 qrtGlobalMCMVariable.isDisableMsg46
		elseif tempType == 47
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer47 qrtGlobalMCMVariable.isDisableMsg47
		elseif tempType == 49
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer49 qrtGlobalMCMVariable.isDisableMsg49
		elseif tempType == 50
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer50 qrtGlobalMCMVariable.isDisableMsg50
		elseif tempType == 103
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer103 qrtGlobalMCMVariable.isDisableMsg103
		elseif tempType == 108
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer108 qrtGlobalMCMVariable.isDisableMsg108
		elseif tempType == 115
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer115 qrtGlobalMCMVariable.isDisableMsg115
		elseif tempType == 116
			call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer116 qrtGlobalMCMVariable.isDisableMsg116
		endif
		set eachRef to GetNextRef
	loop
	;get from variables
	;printtoconsole "ref=%n" eachRef
	;printtoconsole "searchCount=%g" searchCount
	set qrtGlobalMCMVariable.eachRef to eachRef
End