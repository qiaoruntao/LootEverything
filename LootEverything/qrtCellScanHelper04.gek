scn qrtCellScanHelper04

int searchCount
ref eachRef
int totalSearchNum
ref base
int isLegal

Begin Function { searchCount, totalSearchNum}
	;printtoconsole "check 50 now"
	set eachRef to qrtGlobalMCMVariable.eachRef50
	if IsReference eachRef ==0
		;printtoconsole "not a ref now 50"
		set eachRef to GetFirstRef 50 1 0
	endif
	;printtoconsole "eachRef=%n, totalSearchNum=%g, searchCount=%g" eachRef, totalSearchNum, searchCount
	while eachRef != 0 && (totalSearchNum==0 ||searchCount<totalSearchNum)
		let searchCount +=1
		;check the distance
		if qrtGlobalMCMVariable.distanceCheck!=0
			if player.GetDistance eachRef >qrtGlobalMCMVariable.distanceCheck
				;printtoconsole "distanceCheck continue"
				set eachRef to GetNextRef
				continue
			endif
		endif
		;check if dropped by player
		if qrtGlobalMCMVariable.isPickPlayerDroppedItems==0
			if GetSourceModIndex eachRef ==255
				set eachRef to GetNextRef
				continue
			endif
		endif
		;base is used multiple times below
		set base to eachRef.GetBaseObject
		;check if playable
		if qrtGlobalMCMVariable.isPickUnPlayableItem==0
			if IsPlayable base==0
				set eachRef to GetNextRef
				continue
			endif
		endif
		;check if steal
		let isLegal := call qrtIsLegalToPickUpFunction eachRef
		if isLegal==0
			set eachRef to GetNextRef
			continue
		endif
		;check if quest item
		if qrtGlobalMCMVariable.isPickQuestItem==0
			if IsQuestItem base
				set eachRef to GetNextRef
				continue
			endif
		endif
		call qrtSingleKindLootFunction eachRef qrtGlobalMCMVariable.toWhichContainer50 qrtGlobalMCMVariable.isDisableMsg50
		set eachRef to GetNextRef
		;printtoconsole "while check %n now 50" eachRef
	loop
	if eachRef==0
		;all searched, next category
		set qrtGlobalMCMVariable.checkStage to 4
		;printtoconsole "null 50"
		set eachRef to GetFirstRef 50 1 0
	endif
	set qrtGlobalMCMVariable.eachRef50 to eachRef
	SetFunctionValue searchCount
	;printtoconsole "checkStage to 0"
End