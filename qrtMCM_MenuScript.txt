scn qrtMCM_MenuScript

short iMaster
short iMouseover
short iOption
short iTemp
float fTemp
float fValue
reference rList
short iSubMenu

begin GameMode
	if GetGameRestarted
		if IsModLoaded "The Mod Configuration Menu.esp"
			set iMaster to GetModIndex "The Mod Configuration Menu.esp"
			set rList to BuildRef iMaster 2790
			ListAddForm rList qrtMCMMODName
		endif
	endif
end

begin MenuMode 1013
	if IsModLoaded "The Mod Configuration Menu.esp"
	else
		Return
	endif
	if GetUIFloat "StartMenu/MCM/_ActiveMod" == GetModIndex "LootEverything_ForDlcSupport.esp"
		set iOption to GetUIFloat "StartMenu/MCM/_ActiveOption"
		set fValue to GetUIFloat "StartMenu/MCM/_Value"
		if GetUIFloat "StartMenu/MCM/*:5/SubMenu1/_enable" == 0
			SetUIFloat "StartMenu/MCM/_ActiveSubMenu" 1
			SetUIFloat "StartMenu/MCM/*:5/SubMenu1/_enable" 1
			SetUIString "StartMenu/MCM/*:5/SubMenu1/text/string" "Main Menu"
			SetUIString "StartMenu/MCM/*:8/SubTitle1/string" "Main Menu"
		endif
		set iSubMenu to GetUIFloat "StartMenu/MCM/_ActiveSubMenu"
		if GetUIFloat "StartMenu/MCM/_Reset"			;1 - RESET
			SetUIFloat "StartMenu/MCM/_Reset" 0
			;number of columns
			SetUIFloat "StartMenu/MCM/*:1/_columns" 1
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:1/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:1/_title" "Enable/Disable Auto-Looting"
			SetUIFloat "StartMenu/MCM/*:1/*:1/_type" 4
			SetUIFloat "StartMenu/MCM/*:1/*:1/_value" qrtGlobalMCMVariable.isAutoLootEnabled
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:3/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:3/_title" "Hotkey: Add/Remove Current Cell From Auto-Looting"
			SetUIFloat "StartMenu/MCM/*:1/*:3/_type" 3
			SetUIStringEx "StartMenu/MCM/*:1/*:3/value/*:1/string" "%k" qrtGlobalMCMVariable.cellToggleHotkey
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:2/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:2/_title" "Hotkey: Open Main Loot Container"
			SetUIFloat "StartMenu/MCM/*:1/*:2/_type" 3
			SetUIStringEx "StartMenu/MCM/*:1/*:2/value/*:1/string" "%k" qrtGlobalMCMVariable.mainContainerHotkey
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:4/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:4/_title" "Loot into Main Loot Container"
			SetUIFloat "StartMenu/MCM/*:1/*:4/_type" 4
			SetUIFloat "StartMenu/MCM/*:1/*:4/_value" qrtGlobalMCMVariable.isLootIntoContainer
			;option
			SetUIFloat "StartMenu/MCM/*:1/*:5/_enable" 1
			SetUIFloat "StartMenu/MCM/*:1/*:5/_suffix" 1
			SetUIString "StartMenu/MCM/*:1/*:5/_suffixText" "s"
			SetUIString "StartMenu/MCM/*:1/*:5/_title" "Max Loot Distance"
			SetUIFloat "StartMenu/MCM/*:1/*:5/_type" 2.5
			SetUIStringEx "StartMenu/MCM/*:1/*:5/value/*:1/string" "%.1f" qrtGlobalMCMVariable.distanceCheck
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:6/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:6/_title" "Pick player dropped item"
			SetUIFloat "StartMenu/MCM/*:1/*:6/_type" 4
			SetUIFloat "StartMenu/MCM/*:1/*:6/_value" qrtGlobalMCMVariable.isPickPlayerDroppedItems
			;option 
			SetUIFloat "StartMenu/MCM/*:1/*:7/_enable" 1
			SetUIString "StartMenu/MCM/*:1/*:7/_title" "Pick unplayable item(not recommended)"
			SetUIFloat "StartMenu/MCM/*:1/*:7/_type" 4
			SetUIFloat "StartMenu/MCM/*:1/*:7/_value" qrtGlobalMCMVariable.isPickUnPlayableItem
		elseif GetUIFloat "StartMenu/MCM/_Default"		;2 - DEFAULT
			SetUIFloat "StartMenu/MCM/_Default" 0
			SetUIFloat "StartMenu/MCM/_Reset" 1
			;TODO: default values
		elseif GetUIFloat "StartMenu/MCM/_NewValue"		;3 - NEW VALUE
			SetUIFloat "StartMenu/MCM/_NewValue" 0
			SetUIFloat "StartMenu/MCM/_Reset" 1
			;process
			if iSubMenu == 1
				if iOption == 3
					set qrtGlobalMCMVariable.cellToggleHotkey to fValue
				elseif iOption == 2
					set qrtGlobalMCMVariable.mainContainerHotkey to fValue
				elseif iOption == 1
					set qrtGlobalMCMVariable.isAutoLootEnabled to fValue
				elseif iOption == 4
					set qrtGlobalMCMVariable.isLootIntoContainer to fValue
				elseif iOption == 5
					set qrtGlobalMCMVariable.distanceCheck to fValue
				elseif iOption == 6
					set qrtGlobalMCMVariable.isPickPlayerDroppedItems to fValue
				elseif iOption == 7
					set qrtGlobalMCMVariable.isPickUnPlayableItem to fValue
				endif
			endif
		elseif GetUIFloat "StartMenu/MCM/_ShowList" == 1		;4 - SHOW LIST
			SetUIFloat "StartMenu/MCM/_ShowList" 2
		elseif GetUIFloat "StartMenu/MCM/_ShowScale" == 1		;5 - SHOW SCALE
			SetUIFloat "StartMenu/MCM/_ShowScale" 2
			if iSubMenu == 1
				if iOption == 5
					SetUIFloat "StartMenu/MCM/_Value" qrtGlobalMCMVariable.distanceCheck
					SetUIFloat "StartMenu/MCM/_ValueDecimal" 1
					SetUIFloat "StartMenu/MCM/_ValueIncrement" 0.1
					SetUIFloat "StartMenu/MCM/_ValueMax" 500
					SetUIFloat "StartMenu/MCM/_ValueMin" 0
				endif
			endif
		elseif GetUIFloat "StartMenu/MCM/_DefaultScale"		;6 - DEFAULT SCALE
			SetUIFloat "StartMenu/MCM/_DefaultScale" 0
			SetUIFloat "StartMenu/MCM/_ShowScale" 2
			if iSubMenu == 1
				if iOption == 5
					SetUIFloat "StartMenu/MCM/_Value" 1.7
				endif
			endif
		endif
		if iMouseover != GetUIFloat "StartMenu/MCM/*:1/_optionID"		;7 - MOUSE-OVER
			set iMouseover to GetUIFloat "StartMenu/MCM/*:1/_optionID"
			if iMouseover
				SetUIFloat "StartMenu/MCM/MCM_Info/visible" 1
				if iSubMenu == 1
					if iMouseover == 3
						SetUIString "StartMenu/MCM/*:9/string" "Use this key to disable/enable current cell from auto-looting."
					elseif iMouseover == 2
						SetUIString "StartMenu/MCM/*:9/string" "This container will not be looted. It is also used for an alternative container to store looted goods."
					elseif iMouseover == 1
						SetUIString "StartMenu/MCM/*:9/string" "Globally Disable/Enable Auto-Looting"
					elseif iMouseover == 4
						SetUIString "StartMenu/MCM/*:9/string" "Loot All goods into Main Loot Container which you can access it remotely by hotkey."
					elseif iMouseover == 5
						SetUIString "StartMenu/MCM/*:9/string" "Define the maximum distance auto-loot can search. Anything beyond this distance will be ignored. Set it to 0 to disable distance check"
					elseif iMouseover == 6
						SetUIString "StartMenu/MCM/*:9/string" "If set to ON, auto-loot will try picking what you drop."
					elseif iMouseover == 7
						SetUIString "StartMenu/MCM/*:9/string" "If set to ON, auto-loot will try picking up unplayable items."
					endif
				endif
			else
				SetUIFloat "StartMenu/MCM/MCM_Info/visible" 0
			endif
		endif
	endif
end